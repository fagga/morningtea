#!/usr/bin/perl
use strict;
use warnings;

my $config_file = $ARGV[0] || $ENV{HOME}."/.morningtea";
open(F, "<$config_file") or die "Can't read file $config_file: $!";
my @config = <F>;
close(F);
chomp(@config);  # remove trailing newlines

my ($sec,$min,$hour,$mday,$mon,$year,$wday,$yday,$isdst) = localtime(time);
$wday = 7 if($wday == 0);  # week starts with 1/monday and ends with 7/sunday

# start browser
system(shift(@config));

# open links
my $url_cmd = shift(@config);
foreach my $url (read_urls()) {
    my $cmd = $url_cmd;
    $cmd =~ s/%url/$url/g;
    system($cmd);
}


sub read_urls {
    my @urls;
    foreach my $line (@config) {
        if($line =~ /(\S+)\s*:\s*(\S+)/ig) {
            push(@urls, $2) if(pattern_matches($1));
        }
    }
    return @urls;
}

sub pattern_matches {
    my $pattern = shift;

    # every n'th day of the year/month/week
    if($pattern =~ m~^(\d+-\d+|[\d,]+)/(y|m|w)$~) {
        my ($wanted, $today) = ($1, {y=>$yday, m=>$mday, w=>$wday}->{$2});

        if($wanted =~ /^(\d+)$/) {
            return 1 if($today % $wanted == 0);
        }

        if($wanted =~ /^(\d+)-(\d+)$/) {
            return 1 if($today >= $1 and $today <= $2);
        }

        if($wanted =~ /^[\d,]+$/) {
            my @list = split(',', $wanted);
            foreach my $wanted (@list) {
                return 1 if($today == $wanted);
            }
        }
    }

    return 0;
}
